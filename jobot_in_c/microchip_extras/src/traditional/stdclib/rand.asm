; RCS Header $Id: rand.asm,v 1.2 2006/02/04 00:06:53 nairnj Exp $

#include "P18CXXX.INC"
#include "P18MACRO.INC"

; @name rand
;The {\bf rand} function generates a sequence of pseudo-random integer
;values in the range [0,32767].
;The implementation shall behave as if no library function calls {\bf rand}.
;
; int rand (void);


; @name srand
;The {\bf srand} function sets the starting seed for the pseudo-random
;number sequence generated by the {\bf rand} function. If {\bf srand} is
;called again with the same seed value, the same sequence of value shall
;be returned by {\bf rand}. If {\bf rand} is called without {\bf srand}
;having first been called, the pseudo-random sequence of numbers generated
;shall be the same as if {\bf srand} had been called with a seed value
;of 1.
; The implementation shall behave as if no library function calls {\bf srand}.
;
; void srand (unsigned int seed);


          EXTERN  __AARGB3
          EXTERN  __AARGB2
          EXTERN  __AARGB1
          EXTERN  __AARGB0 
          EXTERN  __BARGB1
          EXTERN  __BARGB0          
          extern  FXM1616U
                    
SEED_DATA IDATA
Seed      DB      1,0


STDLIB CODE
srand
  global srand

          Stk2CpyToReg -2,Seed          ; transfer argument to Seed
          return


rand
  global rand

          movff	Seed+1,__AARGB0
          movff	Seed,__AARGB1

          movlw	0x35
          movwf	__BARGB1
          movlw	0x45
          movwf	__BARGB0

          call	FXM1616U

          incf	__AARGB3,F
          clrf	WREG
          addwfc	__AARGB2,F

          bcf       __AARGB2,7            ; limit range to 0 -> 32767
          movff	__AARGB2,Seed+1
          movff	__AARGB3,Seed
	  
	  movff Seed, PRODL
	  movff Seed+1, PRODH
	 
          return

          end

